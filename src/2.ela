#import file;
#import core;

#include "utils.ela"

reports: int[][];
part: int;
current_report: int[];

is_safe :: (skip_index: int) -> bool {
    if skip_index >= current_report.length {
        return false;
    }
    first := 0;
    second := 1;
    if skip_index == 0 {
        first = 1;
        second = 2;
    }
    if skip_index == 1 {
        second = 2;
    }
    increasing := current_report[first] < current_report[second];
    previous := current_report[first];
    for level_index in second..current_report.length {
        level := current_report[level_index];
        if level_index == skip_index {
            continue;
        }
        if (level == previous) ||
            ((previous < level) != increasing) ||
            abs(previous - level) > 3 {
            if part == 1 {
                return false;
            } else {
                return is_safe(skip_index + 1);
            }
        }
        previous = level;
    }
    return true;
}

main :: () {
    part = get_part();
    input := file_read_to_string("input/2.txt");
    rows := split(input, '\n');
    for row in rows {
        if row.length == 0 {
            continue;
        }
        cols := split(row, ' ');
        report : int[];
        for col in cols {
            report ~= atoi(col.data);
        }
        reports ~= report;
    }

    safe_count := 0;
    for report in reports {
        current_report = report;
        if is_safe(-1) {
            safe_count += 1;
        }
    }
    println($"safe count: {safe_count}");
}